plugins {
    id "fabric-loom" version "0.5-SNAPSHOT" apply false
    id "com.github.fudge.forgedflowerloom" version "2.0.0" apply false
    id "maven-publish"
}

def versionEnv = System.getenv("MOD_VERSION")
if (versionEnv == null || versionEnv.isBlank()) {
    versionEnv = "SNAPSHOT"
}

version = "${versionEnv}"

subprojects {
    apply plugin: "fabric-loom"
    apply plugin: "maven-publish"

    group = "${rootProject.group}.${rootProject.archivesBaseName}"
    version = rootProject.version

    sourceSets {
        testmod {
            compileClasspath += main.compileClasspath
            runtimeClasspath += main.runtimeClasspath
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft}"
        mappings "net.fabricmc:yarn:${rootProject.yarn}:v2"
        modImplementation "net.fabricmc:fabric-loader:${rootProject.loader}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric}"

        testmodImplementation sourceSets.main.output
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    afterEvaluate {
        processResources {
            inputs.property "version", project.version

            from(sourceSets.main.resources.srcDirs) {
                include "fabric.mod.json"
                expand "version": project.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude "fabric.mod.json"
            }
        }

        tasks.withType(JavaCompile).configureEach {
            it.options.encoding = "UTF-8"

            def targetVersion = 8
            if (JavaVersion.current().isJava9Compatible()) {
                it.options.release = targetVersion
            } else {
                it.sourceCompatibility = JavaVersion.toVersion(targetVersion)
                it.targetCompatibility = JavaVersion.toVersion(targetVersion)
            }
        }

        java {
            withSourcesJar()
        }

        jar {
            from("LICENSE") {
                rename { "${it}_${project.archivesBaseName}"}
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact(remapJar) {
                    builtBy remapJar
                }
                artifact(sourcesJar) {
                    builtBy remapSourcesJar
                }
            }
        }
    }
}
